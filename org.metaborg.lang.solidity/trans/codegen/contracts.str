module codegen/contracts

imports
  signatures/ebc/-
  signatures/contracts-sig
  signatures/interfaces-sig
  signatures/libraries-sig
  
  codegen/statevariables
  codegen/functions
  codegen/events
  codegen/enums
  codegen/structs
  codegen/functionmodifiers
  
  libspoofax/stratego/debug

rules
  //-----------------------------------------------------------------------------------------------
  //Contract-like
  //-----------------------------------------------------------------------------------------------
  //Interfaces cannot be compiled
  interface-to-ebc: Interface(name, content) -> []
  
  //Treat libraries like contracts
  library-to-ebc: Library(name, content) -> <contract-to-ebc> Contract(name, content)
  
  contract-to-ebc: Contract(name, inherits, content) -> <concat> [
    //Allocate 128 bytes 
    [ PUSH(1, "0x80"),
      PUSH(1, "0x40"),
      MSTORE(),
      PUSH(1, 4),
      CALLDATASIZE(),
      LT(),
      PUSHTAG(tagFailHandler),
      JUMPI(),
      PUSH(1, 0),
      
      //Determine the function being called
      CALLDATALOAD(),
      PUSH(29, "0x100000000000000000000000000000000000000000000000000000000"),
      SWAP(1),
      DIV(),
      PUSH(4, "0xFFFFFFFF"),
      AND() ],
    
    selectFunctionSequence,
    
    //Tag Fail Handler: Revert
    [ JUMPDESTTAG(tagFailHandler),
      PUSH(1, 0),
      DUP(1),
      REVERT() ],
    
    //Function definitions
    functionBlocks
  ] where
    <newname> "FailHandler" => tagFailHandler;
    
    //Create the code for the functions
    funmods   := <filter(?FunctionModifier(_, _, _))> content;
    functions := <filter(?Function(_, _, _, _, _))> content;
    fields    := <filter(?StateVariable(_, _, _, _) + ?ConstantStateVariable(_, _, _, _))> content;
    events    := <filter(?Event(_, _, _))> content;
    enums     := <filter(?Enum(_, _))> content;
    structs   := <filter(?Struct(_, _))> content;
    
    //Compile the function selection sequence.
    //It determines which function is selected upon calling
    selectFunctionSequence := <mapconcat(function-selection(|name))> functions;
    
    //Compile all the functions
    functionBlocks := <mapconcat(fun-to-ebc(|name))> functions
  
  //-----------------------------------------------------------------------------------------------
  //Contract content
  //-----------------------------------------------------------------------------------------------
  //Support statevariables, event, enum and struct definitions
  content-to-ebc(|contractName) = statevar-to-ebc
  content-to-ebc(|contractName) = fun-to-ebc(|contractName)
  content-to-ebc(|contractName) = eventdecl-to-ebc
  content-to-ebc(|contractName) = enumdecl-to-ebc
  content-to-ebc(|contractName) = structdecl-to-ebc
  content-to-ebc(|contractName) = funmoddecl-to-ebc